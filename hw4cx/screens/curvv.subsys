     define(`forloop',
            `pushdef(`$1', `$2')_forloop(`$1', `$2', `$3', `$4')popdef(`$1')')
     define(`_forloop',
            `$4`'ifelse($1, `$3', ,
                   `define(`$1', incr($1))_forloop(`$1', `$2', `$3', `$4')')')
     define(`fordown',
            `pushdef(`$1', `$2')_fordown(`$1', `$2', `$3', `$4')popdef(`$1')')
     define(`_fordown',
            `$4`'ifelse($1, `$3', ,
                   `define(`$1', decr($1))_fordown(`$1', `$2', `$3', `$4')')')
#---------------------------------------------------------------------

define(`CURVV_OUTB_LINE', `
    knob outrb$1 "" onoff "color=blue"   r:outrb$1
')
define(`CURVV_INPB_LINE', `
    disp inprb$1 "" led   "shape=circle" r:inprb$1
')
define(`CURVV_PWRB_LINE', `
    knob pwr_b$1 "" onoff "color=red"    r:pwr_b$1
')
define(`CURVV_TTLB_LINE', `
    disp ttl_b$1 "" led   "shape=circle,color=amber" r:ttl_b$1 tip:"#$1"
')


grouping main CURVV "CURVV" grid "" \
        coltitles:"8b/24b\t7\t6\t5\t4\t3\t2\t1\t0" \
        rowtitles:"OutR\tInpR\tPwrR\tTTL" \
        ncols:9 nattl:2 content:eval(2+9*6) {

    disp   hw_ver    HW    text   "withlabel" dpyfmt:"%-2.0f" r:hw_ver
    disp   sw_ver    SW    text   "withlabel" dpyfmt:"%-2.0f" r:sw_ver

    knob "outr8b"    "8b" text "noincdec" dpyfmt:"%3.0f" r:outr8b
    fordown(`N', 7,   0, `CURVV_OUTB_LINE(N)')
    disp "inpr8b"    "8b" text            dpyfmt:"%3.0f" r:inpr8b
    fordown(`N', 7,   0, `CURVV_INPB_LINE(N)')
    knob "pwr_8b"    "8b" text "noincdec" dpyfmt:"%3.0f" r:pwr_8b
    fordown(`N', 7,   0, `CURVV_PWRB_LINE(N)')
    disp "ttl_24b"    ""  text            dpyfmt:"%8.0f" r:ttl_24b
    fordown(`N', 7,   0, `CURVV_TTLB_LINE(N)')
    noop
    fordown(`N', 15,  8, `CURVV_TTLB_LINE(N)')
    noop
    fordown(`N', 23, 16, `CURVV_TTLB_LINE(N)')
}

wintitle main "CURVV"
winname  main curvv
winclass main CURVV
winopts  main ""
